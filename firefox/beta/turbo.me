meta title="Firefox Beta"
meta namespace="mozilla"
meta name="firefox-beta"

using wget,powershell

cmd  mkdir c:\Workspace
workdir c:\Workspace

batch cmd
 echo [Install] > install.ini
 echo InstallDirectoryPath=c:\FirefoxBeta\ >> install.ini
 echo MaintenanceService=false >> install.ini

var url = "https://download.mozilla.org/?product=firefox-beta-latest&os=win&lang=en-US"

cmd "wget -O firefox.exe --no-check-certificate --no-verbose ""%url%"""
cmd "firefox.exe /INI=c:\Workspace\install.ini"

cmd "echo $Firefox='c:\FirefoxBeta\firefox.exe' >> GetVersion.ps1"
cmd "echo (Get-Item $Firefox ).VersionInfo.ProductVersion >> GetVersion.ps1"
cmd powershell -executionpolicy remotesigned -File GetVersion.ps1
var tag = last

batch
 cd c:\FirefoxBeta
 wget --no-check-certificate --no-verbose -O mozilla.cfg https://raw.githubusercontent.com/pmateusz/browser-automation/master/config/firefox/mozilla.cfg
 cd browser
 wget --no-check-certificate --no-verbose -O override.ini https://raw.githubusercontent.com/pmateusz/browser-automation/master/config/firefox/browser/override.ini
 mkdir defaults\preferences & cd defaults\preferences
 wget --no-check-certificate --no-verbose -O local-settings.js https://raw.githubusercontent.com/pmateusz/browser-automation/master/config/firefox/browser/defaults/preferences/local-settings.js

workdir c:\

cmd rmdir c:\Workspace /s /q
cmd rmdir c:\wget /s /q

meta tag=tag

startup file ("c:\FirefoxBeta\firefox.exe")